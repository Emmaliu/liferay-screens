ext {
	minSdkVersion = 15
	compileSdkVersion = 23
	targetSdkVersion = compileSdkVersion
	buildToolsVersion = '23.0.2'
	versionCode = 4
	versionName = '1.4.0.beta4'
	publishedGroupId = 'com.liferay.mobile'
	artifact = 'liferay-screens'
	libraryName = 'liferay-screens'
	libraryVersion = '1.4.0.beta4'
	libraryDescription = 'A wrapper for Android 6.0 permissions'

	bintrayRepo = 'liferay-screens'
	bintrayName = 'liferay-screens'

	siteUrl = 'https://github.com/liferay/liferay-screens'
	gitUrl = 'https://github.com/liferay/liferay-screens.git'

	developerId = 'nhpatt'
	developerName = 'Javier Gamarra'
	developerEmail = 'nhpatt@gmail.com'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}

def supportLibsVersion = '23.2.0'

ext.libraries = [
		appCompat        : 'com.android.support:appcompat-v7:' + supportLibsVersion,
		recyclerView     : 'com.android.support:recyclerview-v7:' + supportLibsVersion,
		design           : 'com.android.support:design:' + supportLibsVersion,
		junit            : 'junit:junit:4.12',
		sdk              : 'com.liferay.mobile:liferay-android-sdk:7.0.1',
		push             : 'com.liferay.mobile:liferay-push:1.0.7',
		oauth            : 'com.liferay.mobile:liferay-android-oauth:1.3.0',
		eventbus         : 'de.greenrobot:eventbus:2.4.0',
		picasso          : 'com.squareup.picasso:picasso:2.5.2',
		storio           : 'com.pushtorefresh.storio:sqlite:1.8.0',
		storioannotations: 'com.pushtorefresh.storio:sqlite-annotations:1.8.0',
		storioapt        : 'com.pushtorefresh.storio:sqlite-annotations-processor:1.8.0',
		rxjava           : 'io.reactivex:rxjava:1.1.1',
		rxpermissions    : 'com.tbruyelle.rxpermissions:rxpermissions:0.5.2@aar',
		rxbinding        : 'com.jakewharton.rxbinding:rxbinding:0.4.0',
		robolectric      : 'org.robolectric:robolectric:3.0',
		mockito          : 'org.mockito:mockito-core:2.0.43-beta',
]

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.0.0-beta6'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
		classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
		classpath 'com.neenbedankt.gradle.plugins:android-apt:1.6'
	}
}

allprojects {
	repositories {
		jcenter()
		maven { url 'http://dl.bintray.com/tbruyelle/tbruyelle' }
	}
}

//apply from: 'upload.gradle'

subprojects {
	apply plugin: 'com.android.library'
	apply plugin: 'com.github.dcendents.android-maven'
	apply from: '../../install.gradle'
	apply from: '../../bintray.gradle'
}

apply plugin: 'com.android.application'

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	defaultConfig {
		applicationId 'com.liferay.mobile.screens.testapp'
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		versionCode rootProject.ext.versionCode
		versionName rootProject.ext.versionName
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile project(':liferay-screens')
	compile project(':liferay-material-viewset')
	compile project(':liferay-westeros-viewset')
	compile project(':addbookmarkscreenlet')
	compile project(':listbookmarkscreenlet')

	compile libraries.design

	debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
	releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
	testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

	androidTestCompile('com.android.support.test:runner:0.4.1') {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
	androidTestCompile('com.android.support.test:rules:0.4.1') {
		exclude group: 'com.android.support', module: 'support-annotations'
	}

	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
	androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
		exclude group: 'com.android.support', module: 'support-annotations'
		exclude group: 'com.android.support', module: 'support-v4'
		exclude group: 'com.android.support', module: 'recyclerview-v7'
	}
	androidTestCompile('com.android.support.test.espresso:espresso-idling-resource:2.2.1')
}